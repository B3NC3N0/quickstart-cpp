// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  repositories {
    mavenLocal()
    maven { url 'https://maven.google.com'  }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.1'
    classpath 'com.google.gms:google-services:4.0.1'
  }
}

allprojects {
  repositories {
    mavenLocal()
    maven { url 'https://maven.google.com'  }
    jcenter()
  }
}

apply plugin: 'com.android.application'

project.ext {
  // Configure the Firebase C++ SDK location.
  firebase_cpp_sdk_dir = System.getProperty('firebase_cpp_sdk.dir')
  if (firebase_cpp_sdk_dir == null || firebase_cpp_sdk_dir.isEmpty()) {
    firebase_cpp_sdk_dir = System.getenv('FIREBASE_CPP_SDK_DIR')
    if (firebase_cpp_sdk_dir == null || firebase_cpp_sdk_dir.isEmpty()) {
      if ((new File('firebase_cpp_sdk')).exists()) {
        firebase_cpp_sdk_dir = 'firebase_cpp_sdk'
      } else {
        throw new StopActionException(
          'firebase_cpp_sdk.dir property or the FIREBASE_CPP_SDK_DIR ' +
          'environment variable must be set to reference the Firebase C++ ' +
          'SDK install directory.  This is used to configure static library ' +
          'and C/C++ include paths for the SDK.')
      }
    }
  }
  if (!(new File(firebase_cpp_sdk_dir)).exists()) {
    throw new StopActionException(
      sprintf('Firebase C++ SDK directory %s does not exist',
              firebase_cpp_sdk_dir))
  }
}

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'

    sourceSets {
      main {
        jniLibs.srcDirs = ['libs']
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['src/android/java']
        res.srcDirs = ['res']
      }
    }

    defaultConfig {
        applicationId 'com.google.android.messaging.testapp'
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName '1.0'
        externalNativeBuild.ndkBuild {
          arguments "FIREBASE_CPP_SDK_DIR=${project.ext.firebase_cpp_sdk_dir}",
                    "NDK_APPLICTION_MK=jni/Application.mk",
                    sprintf("APP_PLATFORM=android-%d",
                            android.defaultConfig.minSdkVersion.mApiLevel)
        }
    }

    externalNativeBuild.ndkBuild {
        path 'jni/Android.mk'
    }

    buildTypes {
      release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile file(project.ext.firebase_cpp_sdk_dir + "/libs/android/app.pro")
            proguardFile file(project.ext.firebase_cpp_sdk_dir + "/libs/android/messaging.pro")
            proguardFile file('proguard.pro')
      }
    }
}

repositories {
  flatDir {
    dirs project.ext.firebase_cpp_sdk_dir + "/libs/android"
  }
}

dependencies {
  implementation 'com.google.firebase.messaging.cpp:firebase_messaging_cpp@aar'
  implementation 'com.google.firebase:firebase-core:16.0.6'
  implementation 'com.google.firebase:firebase-messaging:17.3.4'
}

apply plugin: 'com.google.gms.google-services'
